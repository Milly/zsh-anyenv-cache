# vim:set ft=zsh:

if [[ -z $ANYENV_ROOT ]]; then
  local roots=(
    ${XDG_DATA_HOME:-${HOME}/.local/share}/anyenv
    ${HOME}/.anyenv
  )
  roots=( ${^${(@)roots}}(N-/) ${(@)roots} )
  ANYENV_ROOT=${roots[1]}
fi
export ANYENV_ROOT

local cmd=${$(whence -p anyenv):-${ANYENV_ROOT}/bin/anyenv}
if [[ ! -x "$cmd" ]]; then
  local OK

  if whence brew >/dev/null; then
    read -q "OK?anyenv command not found, install it from Homebrew? [Y/n]"
    print
    if [[ $OK == [yY] ]]; then
      brew install anyenv || return $?
      rehash
      cmd=$(whence -p anyenv)
    fi
  fi

  if [[ ! -x $cmd ]] && whence git >/dev/null; then
    : ${ANYENV_GIT_URL:="https://github.com/anyenv/anyenv"}
    read -q "OK?install it from '$ANYENV_GIT_URL'? [Y/n]"
    print
    if [[ $OK == [yY] ]]; then
      mkdir -p "$ANYENV_ROOT"
      git clone --depth 1 "${ANYENV_GIT_URL}" "$ANYENV_ROOT" || return $?
      cmd=${ANYENV_ROOT}/bin/anyenv
    fi
  fi

  [[ -x $cmd ]] || { echo "command not found: anyenv"; return 126 }
fi

unfunction anyenv
zsh-anyenv-cache
anyenv "$@"
